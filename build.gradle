configure(subprojects) {
    group = 'org.springframework.samples.distyles'

    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    repositories {
        maven { url "http://repo.springsource.org/libs-milestone" }
    }

    ext.springVersion = '3.2.0.M1'

    dependencies {
        compile "org.springframework:spring-jdbc:${springVersion}"
        compile 'hsqldb:hsqldb:1.8.0.10'
        compile 'log4j:log4j:1.2.16'
        testCompile 'junit:junit:4.9'
    }

    eclipse {
        project {
            natures 'org.springframework.ide.eclipse.core.springnature'
            buildCommand 'org.springframework.ide.eclipse.core.springbuilder'
        }
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main']
            resources.srcDirs = ['src/main']
        }
        test {
            java.srcDirs = ['src/test']
            resources.srcDirs = ['src/test']
        }
    }

    sourceCompatibility=1.6
    targetCompatibility=1.6

    // show all compiler warnings
    [compileJava, compileTestJava]*.options*.compilerArgs = ['-Xlint:all']

    task runTransferScript(type: JavaExec) {
        description = 'Run the TransferScript main() method'
        main = 'com.bank.app.TransferScript'
        classpath = sourceSets.main.runtimeClasspath
    }
}

configure(subprojects - project(':style-0-nospring')) {
    dependencies {
        // for ApplicationContext support
        compile "org.springframework:spring-context:${springVersion}"
    }
}

configure([project(':style-3-autowired'),
           project(':style-4-javaconfig'),
           project(':style-5-hybrid')]) {
    dependencies {
        // for @ContextConfiguration testing support
        testCompile "org.springframework:spring-test:${springVersion}"
    }
}

configure([project(':style-4-javaconfig'),
           project(':style-5-hybrid')]) {
    dependencies {
        // for @Configuration class support
        compile 'cglib:cglib-nodep:2.2'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.0-rc-3'
}
